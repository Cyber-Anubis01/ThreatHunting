Description:
This KQL query detects anomalies in session durations for regular user accounts in Microsoft Sentinel by examining CloudAppEvents. It identifies sessions with AADSessionID that have been active for an unusually long period, exceeding a threshold of 90 days. Prolonged sessions can be exploited by threat actors to maintain persistent access to a compromised account, bypassing typical security measures and increasing the risk of data exfiltration or other malicious activities. This query helps uncover such potential security threats by highlighting unusually long session activities. 

let threshold = 90d; 
CloudAppEvents 
| where AccountType == "Regular" 
| extend RawData = parse_json(RawEventData) 
| extend AADSessionID = RawData.AppAccessContext.AADSessionId 
| summarize  
    FirstSeen = min(TimeGenerated),  
    LastSeen = max(TimeGenerated), 
    Users = make_set(AccountDisplayName) 
    by tostring(AADSessionID) 
| where LastSeen - FirstSeen >= threshold 
| project 
    AADSessionID, 
    Users, 
    FirstSeen, 
    LastSeen, 
    ActiveTime = LastSeen - FirstSeen 
